# Commit Script in file script-commit

# Commit script 
// # Parameter - Generate XML for the script
If XML file exists, it is updated anyway

This script requires the developer to copy the committed script from the side menu before running
# 
# Requires MonkeyBreadSoftware plugin version 10.1+
# 
Perform Script [ Specified: From list ; “MBS Registration” ; Parameter:    ]
If [ Get ( ScriptResult ) ≠ 1 ]
	Exit Script [ Text Result:    ]
End If
# 
# Get Script from clipboard
Set Variable [ $fullxml ; Value: MBS("Clipboard.GetFileMakerData"; "auto") ]
If [ MBS("Clipboard.DetectFileMakerDataType"; $fullxml) ≠ "Script" ]
	Show Custom Dialog [ "Commit Script" ; "Please copy the scripts from the side menu" ]
	Open Script Workspace
	Exit Script [ Text Result:    ]
End If
# 
Set Variable [ $scriptrefs ; Value: MBS("XML.ItemRefs"; $fullxml; "script") ]
Set Variable [ $scriptsDir ; Value: Main::Project path & "/" & Main::Scripts path ]
Loop [ Flush: Always ]
	Exit Loop If [ IsEmpty ( $scriptrefs ) ]
	Set Variable [ $xml ; Value: MBS("XML.SetPathXML"; "<fmxmlsnippet type=\"FMObjectList\"/>"; "fmxmlsnippet¶script"; 4;   GetValue ( $scriptrefs ; 1 ) ) ]
	Set Variable [ $script ; Value: Let ( [   start = Position ( $xml ; "name=\"" ; 1 ; 1 ) + 6 ;   end = Position ( $xml ; "\"" ; start ; 1 ) ] ;   Middle ( $xml ; start ; end - start ) ) ]
	Set Variable [ $scripts ; Value: $scripts & $script & ¶ ]
	Set Variable [ $scriptrefs ; Value: RightValues ( $scriptrefs ; ValueCount ( $scriptrefs ) - 1 ) ]
	# 
	# Commit Script Text
	Set Variable [ $filename ; Value: $script & "." & Main::Script extension ]
	MBS [ Select ; Function: "ScriptWorkspace.OpenScript" ; P1: $script ; P2:    ; P3:    ; P4:    ]
	MBS [ Select ; Function: "ScriptWorkspace.CopyScriptText" ; P1:    ; P2:    ; P3:    ; P4:    ]
	Set Variable [ $fmfn ; Value: MBS("Clipboard.GetText"; "") ]
	Perform Script [ Specified: From list ; “Find File” ; Parameter: $filename & ¶ & $scriptsDir ]
	Set Variable [ $dir ; Value: If ( IsEmpty ( Get ( ScriptResult ) ) ;   Main::Project path & "/" & Main::Scripts path & "/tmp/" ;   Let ( [       path = Get ( ScriptResult ) ;       // Find the last slash to remove the filename       lastSlash = Position ( path ; "/" ; Length ( pat… ]
	Set Variable [ $path ; Value: $dir & $filename ]
	Set Variable [ $path.fm ; Value: ConvertToFileMakerPath ( $path ; 1 ) ]
	Set Error Capture [ On ]
	Delete File [ Target file: “$path.fm” ]
	Set Error Capture [ Off ]
	Set Variable [ $r ; Value: MBS("Files.CreateDirectory"; $dir) ]
	Set Variable [ $export ; Value: MBS("Text.WriteTextFile"; $fmfn; $path; "UTF-8") ]
	// # FileMaker natively exports fields as UTF-16, which Git interprets as bytes, instead of text
	# 
	# XML
	Set Variable [ $createXML ; Value: Get ( ScriptParameter ) = 1 ]
	Set Variable [ $filename ; Value: $script & ".xml" ]
	Set Variable [ $path ; Value: $dir & $filename ]
	Set Variable [ $path.fm ; Value: ConvertToFileMakerPath ( $path ; 1 ) ]
	Set Error Capture [ On ]
	Delete File [ Target file: “$path.fm” ]
	Set Variable [ $exists ; Value: not Get ( LastError ) ]
	Set Error Capture [ Off ]
	If [ $exists or $createXML ]
		# Commit XML
		Set Variable [ $export ; Value: MBS("Text.WriteTextFile"; MBS("XML.Format"; $xml); $path; "UTF-8") ]
	End If
	# 
End Loop
Set Variable [ $release ; Value: MBS("XML.ReleaseAll") ]
Show Custom Dialog [ "Scripts committed" ; Substitute ( Left ( $scripts ; Length ( $scripts ) - 1 ) ; ¶ ; ", " ) ]
Open Script Workspace
# 
// # © 2025 Dean Ayalon <dev@deanayalon.com>
Licensed under the Apache License, Version 2.0
You may use, modify, and distribute this file under the terms of that license.
Please retain this notice and give appropriate credit.

Source: https://github.com/deanayalon/fm-script-commit
Contact me at the above email for any questions or support.
